#!/usr/bin/perl

# get javascript sections out of xml templates
# mengwong@legalese.io 20150704

# then copy and paste the output into http://jsbeautifier.org/
# to see where the unmatched parens are

# and the output of that into http://www.jslint.com/
# to see any other errors.

use strict;
use Getopt::Long;
my %o;
GetOptions(\%o,
		   "equals",
		   "question",
		   "ignorexmldeclarations",
	);

my $input = join("",<>);
warn "input has ", length($input), " chars\n";
while (length $input) {
  if ($input =~ s/^\n+//g) { }
  elsif ($o{ignorexmldeclarations} and $input =~ s/^<\?!=\s*(data.whitespace_handling|data.xml_declaration).*?\?>//s) {
	warn "*** discarding xml declaration $1\n"; }
  elsif ($input !~ /^<\?/ && $input =~ s/^(.+?)(?=<\?)//s) {
	warn "*** discarding text $1\n"; }
  elsif ($input =~ s/^<\?(.*?)\s*\?>//s) {
	my $js = $1;
	if    ($js =~ s/^(!?)=\s*//) { if ($o{equals}) { my $func = $1 ? "noescape" : "  escape"; print "$func( ",$js," );\n" } }
	elsif ($o{question} )   {                   print $js,"\n" }
	if ($js=~tr/(/(/ != $js=~tr/)/)/) { print "// WARNING: the above line may have unbalanced parentheses.\n"; }
  }
  else {
	warn "*** discarding final $input\n";
	last;
  }
}
